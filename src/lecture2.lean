import lecture1

open_locale filter topological_space
open filter

open_locale classical
noncomputable theory

lemma tendsto_of_le_of_le {x y z : ‚Ñï ‚Üí ‚Ñù} {t : ‚Ñù} (hx : tendsto x at_top (ùìù t)) 
  (hz : tendsto z at_top (ùìù t)) (hxy : ‚àÄ n, x n ‚â§ y n) (hyz : ‚àÄ n, y n ‚â§ z n) : 
  tendsto y at_top (ùìù t) :=
begin
  rw tendsto_seq_iff at *,
  intros Œµ hŒµ,
  cases hx (Œµ/2) (half_pos hŒµ) with N‚ÇÅ hN‚ÇÅ,
  cases hz (Œµ/2) (half_pos hŒµ) with N‚ÇÇ hN‚ÇÇ,
  use max N‚ÇÅ N‚ÇÇ,
  intros n hn,
  specialize hxy n,
  specialize hyz n,
  specialize hN‚ÇÅ n (le_of_max_le_left hn),
  specialize hN‚ÇÇ n (le_of_max_le_right hn),
  rw abs_sub_lt_iff at *,
  cases hN‚ÇÅ with hN‚ÇÅ hN‚ÇÅ',
  cases hN‚ÇÇ with hN‚ÇÇ hN‚ÇÇ',
  split;
  linarith,
end

/-
Theorem 1.3 (Bolzano-Weierstrass)

To prove this, we are going to use the proof given in lecture, that is, the one by halving the
interval every time, and showing that we have an infinite number of elements no matter how small
the interval becomes.
-/
namespace bolzano_weierstrass

/-
First, we define the inductive step. That is, given an interval [a, b], this returns either 
  [a, (a+b)/2] or [(a+b)/2, b], depending on whether there is an infinite number of x‚Çô in the first
  interval.
-/
def step (x : ‚Ñï ‚Üí ‚Ñù) (a b : ‚Ñù) : ‚Ñù √ó ‚Ñù :=
if set.infinite {n | x n ‚àà set.Icc a ((a+b)/2)} then
  (a, (a + b)/2)
else
  ((a + b)/2, b)

/-
First, we show that the result of `step` still forms a well defined interval.
-/
lemma step_valid (x : ‚Ñï ‚Üí ‚Ñù) {a b : ‚Ñù} (h : a ‚â§ b) :
  let p := step x a b in
  p.1 ‚â§ p.2 :=
begin
  dsimp [step],
  split_ifs with hp hp; linarith
end

/-
Then, we show that the resulting interval still contains an infinite number of x‚Çôs. To show this,
note that it is not possible for both intervals to be finite, as their union is the original 
interval, and if they are both finite then so is their union. Contradiction.
-/
lemma step_valid'_aux {x : ‚Ñï ‚Üí ‚Ñù} {a b : ‚Ñù} (h : a ‚â§ b) 
  (hx : set.infinite {n | x n ‚àà set.Icc a b}) :
  set.infinite {n | x n ‚àà set.Icc a ((a+b)/2)} ‚à® set.infinite {n | x n ‚àà set.Icc ((a+b)/2) b} :=
begin
  by_contra h,
  push_neg at h,
  rw [set.infinite, set.infinite, not_not, not_not] at h,
  apply hx,
  have : {n | x n ‚àà set.Icc a b} = {n | x n ‚àà set.Icc a ((a+b)/2)} ‚à™ {n | x n ‚àà set.Icc ((a+b)/2) b},
  { ext k,
    simp only [set.mem_union_eq, set.mem_set_of_eq, set.mem_Icc],
    split,
    { rintro ‚ü®h1, h2‚ü©,
      by_cases h' : x k ‚â§ (a + b)/ 2,
      { left, split; linarith },
      { right, split; linarith } },
    { rintro (‚ü®h1,h2‚ü©|‚ü®h1, h2‚ü©);
      split;
      linarith } },
  rw this,
  refine set.finite.union h.1 h.2,
end

lemma step_valid' {x : ‚Ñï ‚Üí ‚Ñù} {a b : ‚Ñù} (h : a ‚â§ b) 
  (hx : set.infinite {n | x n ‚àà set.Icc a b}) :
  let p := step x a b in
  set.infinite {n | x n ‚àà set.Icc p.1 p.2} :=
begin
  dsimp [step],
  split_ifs with hp hp,
  { exact hp },
  { exact or.resolve_left (step_valid'_aux h hx) hp }
end

/-
Next, we show that the new interval is contained within the previous interval
-/
lemma step_valid'' (x : ‚Ñï ‚Üí ‚Ñù) {a b : ‚Ñù} (h : a ‚â§ b) :
  let p := step x a b in
  a ‚â§ p.1 :=
begin
  dsimp [step],
  split_ifs with hp hp,
  { refl },
  { linarith }
end

lemma step_valid''' (x : ‚Ñï ‚Üí ‚Ñù) {a b : ‚Ñù} (h : a ‚â§ b) :
  let p := step x a b in
  p.2 ‚â§ b :=
begin
  dsimp [step],
  split_ifs with hp hp,
  { linarith },
  { refl },
end

/-
Finally, we show that the size of the new interval is half the size of the original interval.
-/
lemma step_valid'''' (x : ‚Ñï ‚Üí ‚Ñù) {a b : ‚Ñù} :
  let p := step x a b in
  p.2 - p.1 = 1/2 * (b - a) :=
begin
  dsimp [step],
  split_ifs with hp hp;
  { linarith },
end

/-
Then, we define the sequence of intervals. [a, b], [a‚ÇÅ, b‚ÇÅ], ...
-/
noncomputable def step_n (x : ‚Ñï ‚Üí ‚Ñù) (a b : ‚Ñù) : ‚Ñï ‚Üí ‚Ñù √ó ‚Ñù
| 0 := (a, b)
| (n + 1) := let p := step_n n in step x p.1 p.2

/-
We can show that the nth term of this is still a well defined sequence, by induction and the fact
that we have already proven this for `step`.
-/
lemma step_n_valid (x : ‚Ñï ‚Üí ‚Ñù) {a b : ‚Ñù} (h : a ‚â§ b) (n : ‚Ñï) :
  (step_n x a b n).1 ‚â§ (step_n x a b n).2 :=
begin
  induction n with k ih,
  { exact h },
  { dsimp [step_n],
    apply step_valid,
    exact ih }
end

/-
We then define two sequences a‚Çô and b‚Çô, which are the lower and upper bounds of those intervals
respectively.
-/
def seq_a (x : ‚Ñï ‚Üí ‚Ñù) (a b : ‚Ñù) (n : ‚Ñï) : ‚Ñù := (step_n x a b n).1
def seq_b (x : ‚Ñï ‚Üí ‚Ñù) (a b : ‚Ñù) (n : ‚Ñï) : ‚Ñù := (step_n x a b n).2

/-
We note that a‚Çô is increasing, that is, a‚ÇÄ ‚â§ a‚ÇÅ ‚â§ a‚ÇÇ ‚â§ ...
-/
lemma seq_a_increasing (x : ‚Ñï ‚Üí ‚Ñù) (a b : ‚Ñù) (ha : a ‚â§ b) : monotone (seq_a x a b) :=
begin
  apply monotone_of_monotone_nat,
  intro n,
  dsimp [seq_a, step_n],
  apply step_valid'',
  apply step_n_valid,
  exact ha,
end

/-
As a corollary, this means that the a‚Çô are bounded below by a‚ÇÄ = a
-/
lemma a_le_seq_a (x : ‚Ñï ‚Üí ‚Ñù) (a b : ‚Ñù) (ha : a ‚â§ b) (n : ‚Ñï) : a ‚â§ seq_a x a b n :=
begin
  exact @seq_a_increasing x a b ha 0 n (nat.zero_le _),
end

/-
We also note that b‚Çô is decreasing, that is b‚ÇÄ ‚â• b‚ÇÅ ‚â• ...
-/
lemma seq_b_decreasing (x : ‚Ñï ‚Üí ‚Ñù) (a b : ‚Ñù) (ha : a ‚â§ b) : 
  ‚àÄ n m, n ‚â§ m ‚Üí seq_b x a b m ‚â§ seq_b x a b n :=
begin
  intros n m h,
  induction h with k ih‚ÇÅ ih‚ÇÇ,
  { refl },
  { dsimp [seq_b, step_n],
    apply le_trans _ ih‚ÇÇ,
    apply step_valid''',
    apply step_n_valid,
    exact ha }
end

/-
And it follows that b‚Çô are bounded above by b‚ÇÄ = b
-/
lemma seq_b_le_b (x : ‚Ñï ‚Üí ‚Ñù) (a b : ‚Ñù) (ha : a ‚â§ b) (n : ‚Ñï) : seq_b x a b n ‚â§ b :=
begin
  exact seq_b_decreasing x a b ha 0 n (nat.zero_le _),
end

/-
An alternative statement for `step_n_valid` is that `a‚Çô ‚â§ b‚Çô` for all `n`.
-/
lemma seq_a_le_seq_b (x : ‚Ñï ‚Üí ‚Ñù) (a b : ‚Ñù) (ha : a ‚â§ b) (n : ‚Ñï) : seq_a x a b n ‚â§ seq_b x a b n :=
step_n_valid x ha n

/-
Then, a‚Çô is an increasing sequence, bounded above by `b`, so it must converge.
-/
lemma is_convergent_seq_a (x : ‚Ñï ‚Üí ‚Ñù) (a b : ‚Ñù) (ha : a ‚â§ b) : is_convergent (seq_a x a b) :=
begin
  apply is_convergent_of_increasing_of_bdd_above b,
  { intro n,
    apply seq_a_increasing,
    exact ha,
    exact nat.le_succ _ },
  { intro n,
    apply le_trans (seq_a_le_seq_b x a b ha n),
    apply seq_b_le_b,
    exact ha }
end

/-
Similarly, b‚Çô is a decreasing sequence that is bounded below, and it must converge as well.
-/
lemma is_convergent_seq_b (x : ‚Ñï ‚Üí ‚Ñù) (a b : ‚Ñù) (ha : a ‚â§ b) : is_convergent (seq_b x a b) :=
begin
  apply is_convergent_of_decreasing_of_bdd_below a,
  { intro n,
    apply seq_b_decreasing,
    exact ha,
    exact nat.le_succ _ },
  { intro n,
    apply le_trans _ (seq_a_le_seq_b _ _ _ _ _),
    apply a_le_seq_a,
    exact ha,
    exact ha }
end

/-
An alternative statement of the halving of the sizes of the inverals is that 
  b‚Çô‚Çä‚ÇÅ - a‚Çô‚Çä‚ÇÅ = 1/2 (b‚Çô - a‚Çô) 
-/
lemma seq_b_succ_sub_seq_a_succ {x : ‚Ñï ‚Üí ‚Ñù} {a b : ‚Ñù} (h : a ‚â§ b) (n : ‚Ñï) :
  seq_b x a b n.succ - seq_a x a b n.succ = 1/2 * (seq_b x a b n - seq_a x a b n) :=
begin
  dsimp [seq_a, seq_b, step_n],
  apply step_valid'''',
end

/-
Then we get that a‚Çô and b‚Çô must tend to the same limit. To see this, suppose if a‚Çô ‚Üí s and b‚Çô ‚Üí t,
then b‚Çô - a‚Çô ‚Üí s - t. As a result, b‚Çô‚Çä‚ÇÅ - a‚Çô‚Çä‚ÇÅ ‚Üí 1/2 (s - t) from the above. However, b‚Çô‚Çä‚ÇÅ - a‚Çô‚Çä‚ÇÅ
is also a subsequence of b‚Çô - a‚Çô, so it must tend to the same limit. Thus, 1/2 (s - t) = (s - t),
and s = t.
-/
lemma seq_a_seq_b_limit_same {x : ‚Ñï ‚Üí ‚Ñù} {a b : ‚Ñù} (h : a ‚â§ b) (s t : ‚Ñù)
  (hs : tendsto (seq_a x a b) at_top (ùìù s))
  (ht : tendsto (seq_b x a b) at_top (ùìù t)) : s = t :=
begin
  have h1 := filter.tendsto.sub ht hs,
  have h2 := @tendsto_subseq _ _ nat.succ (Œª n, nat.lt_succ_self _) h1,
  have h3 : (Œª n, seq_b x a b n - seq_a x a b n) ‚àò nat.succ = (Œª n, 1/2 *(seq_b x a b n - seq_a x a b n)),
  { ext n,
    dsimp,
    rwa seq_b_succ_sub_seq_a_succ },
  rw h3 at h2,
  have h4 := tendsto.const_mul (1/2 : ‚Ñù) h1,
  have h5 := tendsto_at_top_nhds_unique h2 h4,
  linarith,
end

/-
We can then prove that there are infinitely many `n` such that x‚Çô ‚àà [a·µ¢, b·µ¢] for all i.
-/
lemma step_n_valid' {x : ‚Ñï ‚Üí ‚Ñù} {a b : ‚Ñù} (h : a ‚â§ b) (hx : set.infinite {i | x i ‚àà set.Icc a b}) 
  (n : ‚Ñï) : set.infinite {i | x i ‚àà set.Icc (seq_a x a b n) (seq_b x a b n)} :=
begin
  induction n with k ih,
  { exact hx },
  { dsimp [seq_a, seq_b, step_n] at *,
    apply step_valid',
    { apply step_n_valid,
      apply h },
    { exact ih } }
end

/-
From this, the set of such `n` mustn't be bounded above. So no matter gow large `k` is, there is
always an `n > k` such that x‚Çô ‚àà [a·µ¢, b·µ¢]
-/
lemma exists_ge_mem_Icc {x : ‚Ñï ‚Üí ‚Ñù} {a b : ‚Ñù} (h : a ‚â§ b) 
  (hx : set.infinite {i | x i ‚àà set.Icc a b}) (k m : ‚Ñï) : 
  ‚àÉ n > k, x n ‚àà set.Icc (seq_a x a b m) (seq_b x a b m) :=
begin
  have h1 := step_n_valid' h hx m,
  dsimp at h1,
  by_contra h,
  push_neg at h,
  have h2 : {i | x i ‚àà set.Icc (seq_a x a b m) (seq_b x a b m)} ‚äÜ (finset.range (k+1) : set ‚Ñï),
  { intros n hn,
    rw [finset.mem_coe, finset.mem_range],
    by_contra h',
    have h'' : k < n,
    { linarith },
    specialize h n h'',
    apply h,
    exact hn },
  apply h1,
  exact set.finite.subset (finset.finite_to_set _) h2,
end

/-
We define `n·µ¢` such that `n·µ¢‚Çä‚ÇÅ > n·µ¢` and that `x_n·µ¢ ‚àà [a·µ¢, b·µ¢]` for all `i`. We have shown that such
n·µ¢ always exists so this is well defined.
-/
noncomputable def n {x : ‚Ñï ‚Üí ‚Ñù} {a b : ‚Ñù} (h : a ‚â§ b) 
  (hx : set.infinite {i | x i ‚àà set.Icc a b}) : ‚Ñï ‚Üí ‚Ñï
| 0 := nat.find $ exists_ge_mem_Icc h hx 0 0
| (k + 1) := let p := n k in nat.find $ exists_ge_mem_Icc h hx p (k+1)

/-
Finally, we show that `n` satisfies the properties outlined above.
-/
lemma n_spec {x : ‚Ñï ‚Üí ‚Ñù} {a b : ‚Ñù} (h : a ‚â§ b)
  (hx : set.infinite {i | x i ‚àà set.Icc a b}) (k : ‚Ñï) :
  x (n h hx k) ‚àà set.Icc (seq_a x a b k) (seq_b x a b k) :=
begin
  cases k,
  { cases nat.find_spec (exists_ge_mem_Icc h hx 0 0) with h1 h2,
    exact h2 },
  { let p := n h hx k,
    cases nat.find_spec (exists_ge_mem_Icc h hx p (k+1)) with h1 h2,
    dsimp [seq_a, seq_b, step_n, n] at *, -- ok, this `dsimp` is necessary?
    exact h2 }
end

end bolzano_weierstrass

/-
Now that we have this, Bolzano-Weierstrass is fairly straightforward. As each of the 
`x_n·µ¢ ‚àà [a·µ¢, b·µ¢]`, a·µ¢ ‚â§ x_n·µ¢ ‚â§ b·µ¢, and that we've shown that a·µ¢ and b·µ¢ converge to the same limit,
so x_n·µ¢ must converge to the same limit as well.
-/
theorem bolzano_weierstrass {x : ‚Ñï ‚Üí ‚Ñù} (K : ‚Ñù) (hx : ‚àÄ i, |x i| ‚â§ K) :
  ‚àÉ (n : ‚Ñï ‚Üí ‚Ñï), (‚àÄ i, n i < n (i + 1)) ‚àß is_convergent (x ‚àò n) :=
begin
  set a := -K with ha,
  set b := K with ha,
  have hKnonneg : 0 ‚â§ K,
  { linarith [abs_nonneg (x 0), hx 0] },
  have hab : a ‚â§ b := by linarith,
  have hx' : set.infinite {i | x i ‚àà set.Icc a b},
  { convert set.infinite_univ,
    { ext n,
      rw [iff_true, set.mem_Icc],
      specialize hx n,
      rw abs_le at hx,
      split; linarith },
    { exact nat.infinite } },
  use bolzano_weierstrass.n hab hx',
  split,
  { intro i,
    unfold bolzano_weierstrass.n,
    let p := bolzano_weierstrass.n hab hx' i,
    dsimp only,
    cases nat.find_spec (bolzano_weierstrass.exists_ge_mem_Icc hab hx' p (i+1)) with h1 h2,
    exact h1 },
  { cases bolzano_weierstrass.is_convergent_seq_a x a b hab with t ht,
    cases bolzano_weierstrass.is_convergent_seq_b x a b hab with r hr,
    have := bolzano_weierstrass.seq_a_seq_b_limit_same hab t r ht hr,
    rw this at *,
    use r,
    apply tendsto_of_le_of_le ht hr,
    { intro n,
      exact (bolzano_weierstrass.n_spec hab hx' n).1 },
    { intro n,
      exact (bolzano_weierstrass.n_spec hab hx' n).2 } }
end .
