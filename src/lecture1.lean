import topology.instances.real
import topology.basic
import topology.algebra.group
import tactic

open_locale filter topological_space
open filter

notation `|` x `|` := abs x

/-
First, we shall define the definition of convergence of a sequence. mathlib uses filters, so
we will show the filter statement and the common definition are in fact equivalent. We state the
theorems in terms of filters so that we can use the lemmas from mathlib as well when we don't want
to prove them.
-/
lemma tendsto_seq_iff (x : â„• â†’ â„) (c : â„) :
  tendsto x at_top (ð“ c) â†” âˆ€ Îµ > 0, âˆƒ N, âˆ€ n â‰¥ N, |x n - c| < Îµ :=
begin
  split,
  { rintros h Îµ (hÎµ : 0 < Îµ),
    let S := set.Ioo (c - Îµ) (c + Îµ),
    have hS : S âˆˆ ð“ c,
    { apply Ioo_mem_nhds;
      linarith },
    rw tendsto_def at h,
    specialize h S hS,
    rw mem_at_top_sets at h,
    cases h with N hN,
    use N,
    intros n hn,
    specialize hN n hn,
    cases hN with h1 h2,
    rw abs_lt,
    split;
    linarith },
  { intros h S hS,
    rw mem_nhds_iff_exists_Ioo_subset at hS,
    rcases hS with âŸ¨l, u, âŸ¨h1, h2âŸ©, h3âŸ©,
    let Îµ := min (c - l) (u - c),
    have hÎµ : 0 < Îµ,
    { apply lt_min;
      linarith },
    cases h Îµ hÎµ with N hN,
    rw [mem_map, mem_at_top_sets],
    use N,
    intros n hn,
    specialize hN n hn,
    rw abs_lt at hN,
    cases hN,
    apply h3,
    split,
    { have : Îµ â‰¤ c - l,
      { exact min_le_left _ _ },
      linarith },
    { have : Îµ â‰¤ u - c,
      { exact min_le_right _ _ },
      linarith } }
end .

/-
Definition of the Supremum

stuff
-/

/--
We say that a sequence xâ‚™ is convergent if there exists a such that xâ‚™ â†’ a as n â†’ âˆž
-/
def is_convergent (x : â„• â†’ â„) := âˆƒ a, tendsto x at_top (ð“ a)

lemma is_convergent_of_increasing_of_bdd_above {x : â„• â†’ â„} (A : â„) (hxâ‚ : âˆ€ n, x n â‰¤ x (n + 1)) 
  (hxâ‚‚ : âˆ€ n, x n â‰¤ A) : is_convergent x :=
begin
  set c := Sup (x '' set.univ) with hc,
  use c,
  rw tendsto_seq_iff,
  intros Îµ hÎµ,
  have : âˆƒ N, c - Îµ < x N,
  { by_contra h,
    push_neg at h,
    have := @real.Sup_le_ub (x '' set.univ) _ (c - Îµ) _,
    { linarith },
    use [x 0, 0, set.mem_univ 0],
    rintros y âŸ¨N, hNâ‚, rflâŸ©,
    apply h },
  cases this with N hN,
  use N,
  intros n hn,
  have hxn : x N â‰¤ x n,
  { exact monotone_of_monotone_nat hxâ‚ hn },
  have hxn' : x n â‰¤ c,
  { apply real.le_Sup _ _ _,
    { use A,
      rintros y âŸ¨k, hk, rflâŸ©,
      apply hxâ‚‚ },
    use [n, set.mem_univ n] },
  rw abs_sub_lt_iff,
  split;
  linarith
end

lemma is_convergent_of_decreasing_of_bdd_below {x : â„• â†’ â„} (A : â„) (hxâ‚ : âˆ€ n, x (n + 1) â‰¤ x n) 
  (hxâ‚‚ : âˆ€ n, A â‰¤ x n) : is_convergent x := 
begin
  set c := Inf (x '' set.univ) with hc,
  use c,
  rw tendsto_seq_iff,
  intros Îµ hÎµ,
  have : âˆƒ N, x N < c + Îµ,
  { by_contra h,
    push_neg at h,
    have := @real.lb_le_Inf (x '' set.univ) _ (c + Îµ) _,
    { linarith },
    use [x 0, 0, set.mem_univ 0],
    rintros y âŸ¨N, hNâ‚, rflâŸ©,
    apply h },
  cases this with N hN,
  use N,
  intros n hn,
  have hxn : x n â‰¤ x N,
  { induction hn with k ihâ‚ ihâ‚‚,
    { refl },
    { exact le_trans (hxâ‚ _) ihâ‚‚ } },
  have hxn' : c â‰¤ x n,
  { apply real.Inf_le,
    { use A,
      rintros y âŸ¨k, hk, rflâŸ©,
      apply hxâ‚‚ },
    use [n, set.mem_univ n] },
  rw abs_sub_lt_iff,
  split;
  linarith
end

/-
Lemma 1.1 (i)

Limits are unique. If xâ‚™ â†’ a and xâ‚™ â†’ b, then a = b
-/
lemma tendsto_at_top_nhds_unique {x : â„• â†’ â„} {a b : â„} (ha : tendsto x at_top (ð“ a)) 
  (hb : tendsto x at_top (ð“ b)) : a = b :=
begin
  have key : âˆ€ Îµ > 0, |a - b| â‰¤ 2 * Îµ,
  { rintros Îµ (hÎµ : 0 < Îµ),
    rw tendsto_seq_iff at *,
    cases ha Îµ hÎµ with Nâ‚ hNâ‚,
    cases hb Îµ hÎµ with Nâ‚‚ hNâ‚‚,
    let N := max Nâ‚ Nâ‚‚,
    specialize hNâ‚ N (le_max_left _ _),
    specialize hNâ‚‚ N (le_max_right _ _),
    rw [show a - b = (a - x N) + (x N - b), by ring],
    refine le_trans (abs_add _ _) _,
    rw abs_sub,
    linarith },
  by_contra hc,
  set Îµ := |a - b|/3 with hÎµdef,
  have hab: 0 < |a - b|,
  { rwa [abs_pos, sub_ne_zero] },
  have hÎµ : 0 < Îµ,
  { linarith },
  specialize key Îµ hÎµ,
  rw hÎµdef at key,
  linarith,
end

/-
Lemma 1.1 (ii)

If xâ‚™ â†’ a, and x_{n_j} is a subsequence, then it will also tend to a
-/
lemma tendsto_subseq {x : â„• â†’ â„} {a : â„} {n : â„• â†’ â„•} (hn : âˆ€ k, n k < n (k+1)) 
  (hx : tendsto x at_top (ð“ a)) : tendsto (x âˆ˜ n) at_top (ð“ a) :=
begin
  have : âˆ€ k, k â‰¤ n k,
  { intro k,
    induction k with d ih,
    { exact nat.zero_le _ },
    exact lt_of_le_of_lt ih (hn _) },
  rw tendsto_seq_iff at *,
  intros Îµ hÎµ,
  cases hx Îµ hÎµ with N hN,
  use N,
  intros k hk,
  rw function.comp_apply,
  apply hN,
  exact le_trans hk (this _),
end

/-
Lemma 1.1 (iii)

If xâ‚™ = c for all n, then xâ‚™ â†’ c

NB: This is in mathlib as `tensto_const_nhds`
-/
lemma tendsto_const_at_top {c : â„} :
  tendsto (Î» n : â„•, c) at_top (ð“ c) :=
begin
  intros s hs,
  rw [mem_map, mem_at_top_sets],
  use 0,
  intros b hb,
  rw set.mem_set_of_eq,
  exact mem_of_nhds hs
end

/-
Lemma 1.1 (iv)

If xâ‚™ â†’ a and yâ‚™ â†’ b, then xâ‚™ + yâ‚™ â†’ a + b

NB: This is in mathlib as `filter.tendsto.add`
-/
lemma tendsto_add_at_top_nhds {x y : â„• â†’ â„} {a b : â„} (hx : tendsto x at_top (ð“ a)) 
  (hy : tendsto y at_top (ð“ b)) : tendsto (x + y) at_top (ð“ (a + b)) :=
begin
  rw tendsto_seq_iff at *,
  intros Îµ hÎµ,
  cases hx (Îµ/2) (half_pos hÎµ) with Nâ‚ hNâ‚,
  cases hy (Îµ/2) (half_pos hÎµ) with Nâ‚‚ hNâ‚‚,
  use max Nâ‚ Nâ‚‚,
  intros n hn,
  specialize hNâ‚ n (le_trans (le_max_left _ _) hn),
  specialize hNâ‚‚ n (le_trans (le_max_right _ _) hn),
  rw [pi.add_apply, show x n + y n - (a + b) = (x n - a) + (y n - b), by ring],
  refine lt_of_le_of_lt (abs_add _ _) _,
  linarith,
end

/-
Lemma 1.1 (v)

If xâ‚™ â†’ a and yâ‚™ â†’ b, then xâ‚™yâ‚™ â†’ ab

NB: This is in mathlib as `filter.tendsto.mul`
-/
lemma tendsto_mul_at_top_nhds {x y : â„• â†’ â„} {a b : â„} (hx : tendsto x at_top (ð“ a)) 
  (hy : tendsto y at_top (ð“ b)) : tendsto (x * y) at_top (ð“ (a * b)) :=
begin
  rw tendsto_seq_iff at *,
  intros Îµ hÎµ,
  have h1 : 0 < |a| + |b| + 1,
  { linarith [abs_nonneg a, abs_nonneg b] },
  have hÎµ' : 0 < Îµ / (|a| + |b| + 1) := div_pos hÎµ h1,
  cases hx 1 zero_lt_one with Nâ‚ hNâ‚,
  cases hx _ hÎµ' with Nâ‚‚ hNâ‚‚,
  cases hy _ hÎµ' with Nâ‚ƒ hNâ‚ƒ,
  use max (max Nâ‚ Nâ‚‚) Nâ‚ƒ,
  intros n hn,
  rw [pi.mul_apply, show x n * y n - a * b = x n * (y n - b) + (x n - a) * b, by ring],
  refine lt_of_le_of_lt (abs_add _ _) _,
  rw [abs_mul, abs_mul],
  specialize hNâ‚ n (le_trans (le_max_left _ _) (le_trans (le_max_left (max Nâ‚ Nâ‚‚) Nâ‚ƒ) hn)),
  specialize hNâ‚‚ n (le_trans (le_max_right _ _) (le_trans (le_max_left (max Nâ‚ Nâ‚‚) Nâ‚ƒ) hn)),
  specialize hNâ‚ƒ n (le_trans (le_max_right _ _) hn),
  have : |x n| < |a| + 1,
  { rw abs_lt at hNâ‚ âŠ¢,
    cases hNâ‚ with h1 h2,
    by_cases ha : 0 â‰¤ a,
    { rw abs_of_nonneg ha,
      split; linarith },
    { rw not_le at ha,
      rw abs_of_neg ha,
      split; linarith } },
  calc |x n| * |y n - b| + |x n - a| * |b| â‰¤ (|a| + 1) * |y n - b| + |x n - a| * |b| : _
                                       ... < (|a| + 1) * (Îµ / (|a| + |b| + 1)) + Îµ / (|a| + |b| + 1) * |b| : _
                                       ... = Îµ : _,
  { apply add_le_add_right,
    apply mul_le_mul,
    { exact le_of_lt this },
    { refl },
    { exact abs_nonneg _ },
    { linarith [abs_nonneg a] } },
  { apply add_lt_add_of_lt_of_le,
    { rwa mul_lt_mul_left,
      linarith [abs_nonneg a] },
    { apply mul_le_mul,
      { exact le_of_lt hNâ‚‚ },
      { refl },
      { exact abs_nonneg _ },
      { exact le_of_lt (lt_of_le_of_lt (abs_nonneg _) hNâ‚‚) } } },
    { field_simp,
      apply div_eq_of_eq_mul,
      { linarith },
      ring }
end .

/-
Lemma 1.1 (vi)

If xâ‚™ â†’ a, âˆ€ n, xâ‚™ â‰  0, a â‰  0 then 1/aâ‚™ â†’ 1/a

NB: This is in mathlib as `filter.tendsto.inv`
-/
lemma tendsto_inv_at_top_nhds {x : â„• â†’ â„} {a : â„} (hxâ‚ : âˆ€ n, x n â‰  0) (ha : a â‰  0)
  (hxâ‚‚ : tendsto x at_top (ð“ a)) : tendsto xâ»Â¹ at_top (ð“ aâ»Â¹) :=
begin
  rw tendsto_seq_iff at *,
  intros Îµ hÎµ,
  have hÎµ' : 0 < |a|^2 * Îµ / 2,
  { apply div_pos,
    { apply mul_pos,
      { apply pow_two_pos_of_ne_zero,
        apply ne_of_gt,
        apply abs_pos.mpr â€¹_â€º },
      { exact hÎµ } },
    { norm_num } },
  cases hxâ‚‚ (|a|/2) (div_pos (abs_pos.mpr ha) zero_lt_two) with Nâ‚ hNâ‚,
  have hNâ‚ : âˆ€ n â‰¥ Nâ‚, |a|/2 â‰¤ |x n|,
  { intros n hn,
    specialize hNâ‚ n hn,
    rw abs_sub_lt_iff at hNâ‚,
    by_cases hx : 0 â‰¤ x n; [rw abs_of_nonneg hx at *, { rw not_le at hx, rw abs_of_neg hx at * }];
    { by_cases ha' : 0 â‰¤ a,
      { rw [abs_of_nonneg ha'] at *,
        linarith },
      { rw [not_le] at ha',
        rw [abs_of_neg ha'] at *,
        linarith } } },
  cases hxâ‚‚ _ hÎµ' with Nâ‚‚ hNâ‚‚,
  use max Nâ‚ Nâ‚‚,
  intros n hn,
  specialize hNâ‚ n (le_of_max_le_left hn),
  rw div_le_iff at hNâ‚,
  specialize hNâ‚‚ n (le_of_max_le_right hn),
  rw [pi.inv_apply, inv_sub_inv (hxâ‚ _) ha, abs_div, abs_sub, div_lt_iff, abs_mul],
  calc |x n - a| < |a|^2 * Îµ / 2 : hNâ‚‚
             ... â‰¤ Îµ * (|x n| * |a|) : _,
  rwa [div_le_iff, pow_two, mul_comm, mul_assoc, mul_le_mul_left hÎµ, mul_right_comm, 
      mul_le_mul_right (abs_pos.mpr ha)],
  { norm_num },
  { rw abs_mul,
    apply mul_pos,
    { exact abs_pos.mpr (hxâ‚ n) },
    { exact abs_pos.mpr ha } },
  { norm_num },
end .

/-
Lemma 1.1 (vii)

If xâ‚™ â‰¤ A, and xâ‚™ â†’ a, then a â‰¤ A 
-/
lemma tendsto_lim_le_of_le {x : â„• â†’ â„} {a A : â„} (hxâ‚ : âˆ€ n, x n â‰¤ A) 
  (hxâ‚‚ : tendsto x at_top (ð“ a)) : a â‰¤ A :=
begin
  by_contra h,
  rw not_le at h,
  set Îµ := a - A with hÎµ,
  have hÎµ' : 0 < Îµ,
  { linarith },
  rw tendsto_seq_iff at hxâ‚‚,
  specialize hxâ‚‚ Îµ hÎµ',
  cases hxâ‚‚ with N hN,
  specialize hN N (le_refl _),
  rw abs_sub_lt_iff at hN,
  cases hN,
  specialize hxâ‚ N,
  linarith,
end

/-
Lemma 1.2

1/n â†’ 0
-/
lemma tensto_one_div : tendsto (Î» n, 1/(n + 1) : â„• â†’ â„) at_top (ð“ 0) :=
begin
  have h1 : âˆƒ a, tendsto (Î» n, 1/(n + 1) : â„• â†’ â„) at_top (ð“ a) := is_convergent_of_decreasing_of_bdd_below 0 _ _,
  cases h1 with a ha,
  have h2 : tendsto (Î» n, 1/2 * (1/(n+1)) : â„• â†’ â„) at_top (ð“ (1/2 * a)),
  { apply filter.tendsto.mul,
    { exact tendsto_const_nhds },
    { exact ha } },
  have h3 : tendsto (Î» n, 1/2 * (1/(n+1)) : â„• â†’ â„) at_top (ð“ a),
  { have : (Î» n, 1/2 * (1/(n+1) : â„) : â„• â†’ â„) = (Î» n, 1/(n + 1) : â„• â†’ â„) âˆ˜ (Î» n, 2 * n + 1 : â„• â†’ â„•),
    { ext n,
      simp only [â†mul_inv', mul_add, one_div, mul_one, nat.cast_bit0, inv_inj', nat.cast_add, nat.cast_one, nat.cast_mul],
      linarith, },
    rw this,
    apply tendsto_subseq,
    { intro k, omega },
    { exact ha } },
  have h4 : a = 1/2 * a,
  { exact tendsto_at_top_nhds_unique h3 h2 },
  have h5 : a = 0,
  { linarith },
  rw h5 at *,
  exact ha,
  { intro n,
    rw one_div_le_one_div,
    { norm_num },
    { linarith [@nat.cast_add â„ _ _ n 1, @nat.cast_one â„ _ _, @nat.cast_nonneg â„ _ n] },
    { linarith [@nat.cast_nonneg â„ _ n] } },
  intro n,
  apply le_of_lt,
  apply div_pos,
  { exact zero_lt_one },
  { linarith [@nat.cast_nonneg â„ _ n] },
end .

/-
Remark: The definition of convergence of a sequence holds just as well for â„‚ as it does for â„, that
is, we can define the convergence of xâ‚™ âˆˆ â„‚ just as well as xâ‚™ âˆˆ â„
-/
